def pomBaseData = {
    licenses {
        license {
            name "Apache License, Version 2.0"
            url "https://www.apache.org/licenses/LICENSE-2.0.txt"
        }
    }
    scm {
        url "https://github.com/dbaelz/Konclik"
    }
}

def generatePom = { pom ->
    pom.withXml {
        def root = it.asNode()
        root.appendNode('name', project.name)
        root.appendNode('description', 'Kotlin/Native Command Line Interface Kit')
        root.appendNode('url', 'https://github.com/dbaelz/Konclik')
        root.children().last() + pomBaseData
    }
}

ext.generatePom = generatePom

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publish = false
    override = true

    pkg {
        userOrg = 'dbaelz'
        repo = 'konclik'
        name = 'library'
        licenses = ['Apache-2.0']
        publications = ['mavenProject']
        vcsUrl = 'https://github.com/dbaelz/Konclik.git'

        version {
            released = new Date()
            vcsTag = project.version
        }
    }
}