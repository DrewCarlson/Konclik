buildscript {
    ext.kotlin_version = '1.2.70'
    ext.kotlin_native_plugin_version = '0.9'
    ext.bintray_plugin_version = '1.8.4'

    repositories {
        jcenter()
        maven {
            url 'https://cache-redirector.jetbrains.com/maven-central'
        }
        maven {
            url "https://dl.bintray.com/jetbrains/kotlin-native-dependencies"
        }
        maven {
            url "http://dl.bintray.com/kotlin/kotlin-eap"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:$kotlin_native_plugin_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintray_plugin_version"
    }
}

// Global configuration for all Konclik library submodules
def libs = ['native', 'jvm']
configure(allprojects.findAll { libs.contains(it.name) }) {
    group 'de.dbaelz.konclik'
    version '0.5.0'

    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
    apply from: rootProject.file('publish.gradle')

    publishing {
        publications {
            mavenProject(MavenPublication) {
                if (project.name == 'jvm') {
                    from components.java
                    artifact sourcesJar
                }

                groupId project.group
                artifactId project.name
                version project.version

                generatePom(pom)
            }
        }
    }
}
